apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "tun.proxy"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 100015
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -fvisibility=hidden "

                arguments "-DCMAKE_VERBOSE_MAKEFILE=1 -DANDROID_FUNCTION_LEVEL_LINKING=ON"
            }
        }
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        debug {
        }
        release {
            // @See gradle.properties
            storeFile file(productKeyStore)
            keyAlias productKeyAlias
            storePassword productKeyStorePassword
            keyPassword productKeyAliasPassword
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
         abortOnError false
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name.equals("release")) {
            variant.outputs.all {
                if (outputFileName != null && outputFileName.endsWith('.apk')) {
                    def versionName = defaultConfig.versionName
                    outputFileName = "${APP_NAME}_v${versionName}.apk"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
